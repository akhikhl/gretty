buildscript {
  repositories {
    mavenLocal()
    jcenter()
  }

  dependencies {
    classpath 'org.apache.commons:commons-lang3:3.4'
    classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7'
    classpath 'org.jfrog.buildinfo:build-info-extractor-gradle:3.1.0'
  }
}

apply plugin: 'base' // import clean task
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.artifactory'

ext {
  project_website = "https://github.com/${developerId}/${projectId}"
  vcs_url = "${project_website}.git"
  project_scm = "scm:git@github.com:${developerId}/${projectId}.git"
  license_url = "https://raw.github.com/${developerId}/${projectId}/master/LICENSE"

  if( !has( 'bintrayUser' ) )
    bintrayUser = ''

  if( !has( 'bintrayKey' ) )
    bintrayKey = ''

  if( !has( 'bintrayKeyPhrase' ) )
    bintrayKeyPhrase = ''
    
  if( !has( 'bintrayDryRun' ) )
    bintrayDryRun = false
}

task('build')

task('buildExamples', type: GradleBuild) {
  dir = file('examples')
  tasks = [ 'build' ]
  project.tasks.build.finalizedBy it
}

task('testAll', dependsOn: build)

task('testAllExamples', type: GradleBuild) {
  dependsOn buildExamples
  dir = file('examples')
  tasks = [ 'testAll' ]
  project.tasks.testAll.finalizedBy it
}

task('cleanExamples', type: GradleBuild) {
  dir = file('examples')
  tasks = [ 'clean' ]
  project.tasks.clean.dependsOn it
}

task('wrapper', type: Wrapper) {
  gradleVersion = '2.4'
}

artifactory {
  contextUrl = 'https://oss.jfrog.org/artifactory'
  publish {
    repository {
      if (project.version.endsWith('-SNAPSHOT'))
        repoKey = 'oss-snapshot-local'
      else
        repoKey = 'oss-release-local'
      username = project.bintrayUser
      password = project.bintrayKey
    }
  }
  resolve {
    repository {
      repoKey = 'repo'
    }
  }
}

