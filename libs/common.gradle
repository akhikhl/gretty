// include this file in subprojects:
// apply from: rootProject.file('libs/common.gradle')

apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.artifactory'
apply plugin: 'com.jfrog.bintray'

repositories {
  mavenLocal()
  jcenter()
}

group = rootProject.group
version = rootProject.version

configurations {
  providedCompile
  compile.extendsFrom providedCompile
}

afterEvaluate {

dependencies {
  testCompile "org.codehaus.groovy:groovy-all:${groovy_version}"
  testCompile "org.spockframework:spock-core:${spock_version}"
}

sourceCompatibility = '1.6'
targetCompatibility = '1.6'

rootProject.tasks.buildExamples.dependsOn project.tasks.build

task sourcesJar(type: Jar, dependsOn: classes, description: 'Creates sources jar') {
  classifier = 'sources'
  from sourceSets.main.allSource
}

task javadocJar(type: Jar, description: 'Creates javadoc jar') {
  dependsOn javadoc
  classifier = 'javadoc'
  from javadoc.destinationDir
  if(tasks.findByName('groovydoc')) {
    dependsOn groovydoc
    from groovydoc.destinationDir
  }
}

artifacts {
  archives sourcesJar, javadocJar
}

def thisProject = project

def configurePom = {
  resolveStrategy = Closure.DELEGATE_FIRST
  packaging 'jar'
  description thisProject.description
  url thisProject.project_website

  scm {
    url thisProject.project_scm
    connection thisProject.project_scm
    developerConnection thisProject.project_scm
  }

  licenses {
    license {
      name thisProject.license
      url thisProject.license_url
      distribution 'repo'
    }
  }

  developers {
    developer {
      id thisProject.developerId
      name thisProject.developerName
    }
  }
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java
      pom.withXml {
        asNode().children().last() + configurePom
      }
    }
  }
}

install.repositories.mavenInstaller.pom*.whenConfigured { pom ->
  pom.project {
    name thisProject.name
    configurePom.rehydrate(delegate, owner, thisObject).call()
  }
}

// lib projects should be always installed into "$HOME/.m2"
project.tasks.build.finalizedBy project.tasks.install
rootProject.tasks.buildExamples.dependsOn project.tasks.install

artifactoryPublish {
  dependsOn { project.tasks.generatePomFileForMavenJavaPublication }
  publications 'mavenJava'
}

bintray {
  user = project.bintrayUser
  key = project.bintrayKey
  configurations = ['archives']
  publications = ['mavenJava']
  
  pkg {
    repo = 'maven'
    name = "${thisProject.projectId}-fork"
    desc = thisProject.description
    licenses = [ 'MIT' ]
    vcsUrl = thisProject.vcs_url
    labels = thisProject.projectLabels.split(',')
  }
  
  dryRun = thisProject.bintrayDryRun
  publish = true
}

bintrayUpload.dependsOn assemble

if(!bintrayDryRun)
  bintrayUpload.finalizedBy parent.tasks.bintraySign

} // afterEvaluate
