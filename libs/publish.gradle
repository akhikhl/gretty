// include this file in subprojects:
// apply from: rootProject.file('libs/publish.gradle')

apply plugin: 'signing'
apply plugin: 'maven-publish'
apply plugin: 'bintray'

import org.gradle.api.internal.artifacts.publish.DefaultPublishArtifact

def thisProject = project

install.repositories.mavenInstaller.pom*.whenConfigured { pom ->
  pom.project {
    name thisProject.name
    packaging 'jar'
    description thisProject.project_description
    url thisProject.project_website

    scm {
      url thisProject.project_scm
      connection thisProject.project_scm
      developerConnection thisProject.project_scm
    }

    licenses {
      license {
        name thisProject.license
        url thisProject.license_url
        distribution 'repo'
      }
    }

    developers {
      developer {
        id thisProject.developer_id
        name thisProject.developer_name
      }
    }
  }
}

bintray {
  user = project.hasProperty('bintrayUser') ? project.bintrayUser : ''
  key = project.hasProperty('bintrayKey') ? project.bintrayKey : ''
  configurations = ['archives']
  pkg {
    repo = 'maven'
    name = thisProject.rootProject.project_id
    desc = thisProject.rootProject.description
    licenses = ['MIT']
    labels = ['gretty', 'jetty', 'gradle', 'plugin']
  }
  dryRun = false
}

bintrayUpload {
  doFirst {
    def signatureArtifacts = []
    collectArtifacts(project.configurations.archives).each { a ->
      def s = signing.sign(a.file)
      s.signatureFiles.each { f ->
        def fname = f.name
        def ext = fname.substring(fname.lastIndexOf('.') + 1)
        signatureArtifacts.add(new DefaultPublishArtifact(a.name, a.extension + '.' + ext, ext, a.classifier, new Date(), f, [] as Object[]))
      }
    }
    project.configurations.archives.artifacts.addAll(signatureArtifacts)
  }
}

