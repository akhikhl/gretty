apply plugin: 'groovy'
apply plugin: 'org.akhikhl.gretty'
apply from: rootProject.file('integrationTests.gradle')

dependencies {
  compile "org.codehaus.groovy:groovy-all:$groovy_version"
  compile 'org.webjars:bootstrap:3.2.0'
  compile 'org.webjars:jquery:2.1.1'
}

gretty {
  springBoot = true
}

farm {
  webapp project
  webapp ':spring-boot-farm:spring-boot-webservice'
  webapp ':spring-boot-farm:jee-webservice'
  webapp ':spring-boot-farm:spring-web-mvc-webservice'
}

ext {
  integrationTestContainers = ['jetty8', 'jetty9', 'tomcat7', 'tomcat8']
}

task 'farmIntegrationTestAllContainers'

def containers = ext.integrationTestContainers
containers.each { container ->
  farms.farm container, {
    servletContainer = container
    webapp project
    webapp ':spring-boot-farm:spring-boot-webservice'
    webapp ':spring-boot-farm:jee-webservice'
    afterEvaluate {
      tasks['farmBeforeIntegrationTest' + container].integrationTestTask 'integrationTest_' + container
      tasks['farmAfterIntegrationTest' + container].integrationTestTask 'integrationTest_' + container
      tasks['farmIntegrationTest' + container].integrationTestTask 'integrationTest_' + container
      tasks.farmIntegrationTestAllContainers.dependsOn tasks['farmIntegrationTest' + container]
    }
  }
}

rootProject.tasks.testAll.dependsOn tasks.farmIntegrationTestAllContainers
