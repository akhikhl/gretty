apply plugin: 'war'
apply plugin: 'org.akhikhl.gretty'
apply from: rootProject.file('integrationTests.gradle') // remove this to disable integration tests

import org.akhikhl.gretty.ServletContainerConfig

dependencies {
  compile 'org.webjars:bootstrap:3.2.0'
  compile 'org.webjars:jquery:2.1.1'
  // We use UrlRewriteFilter for redirecting to the protected home page.
  compile 'org.tuckey:urlrewritefilter:4.0.4'
}

gretty {
  httpEnabled = false
  httpsEnabled = true
  servletContainer = 'jetty9'
  realm = 'auth'
  realmConfigFile = '../security'
  singleSignOn = true
}

farm {
  webapp project
  webapp ':secureFarm:MyWebService'
}

task 'farmIntegrationTestAllContainers'

def containers = ServletContainerConfig.getConfigNames()
containers.each { container ->
  farms.farm container, {
    servletContainer = container
    webapp project
    webapp ':secureFarm:MyWebService'
    afterEvaluate {
      tasks['farmBeforeIntegrationTest' + container].integrationTestTask 'integrationTest_' + container
      tasks['farmAfterIntegrationTest' + container].integrationTestTask 'integrationTest_' + container
      tasks['farmIntegrationTest' + container].integrationTestTask 'integrationTest_' + container
      tasks.farmIntegrationTestAllContainers.dependsOn tasks['farmIntegrationTest' + container]
    }
  }
}

rootProject.tasks.testAll.dependsOn tasks.farmIntegrationTestAllContainers
